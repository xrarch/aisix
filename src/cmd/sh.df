#include "<df>/dragonfruit.h"
#include "<ll>/rtaisix/aisix.h"

buffer Prompt 256
buffer PName 256
buffer PComp 256
buffer Argvt (32 4 *)

fn Main { ... -- ret }
	TTYI_IGN TTYI_CHILD_ALL | SetTTYIgnore drop

	0 ret!
	
	while (1)
		"# " Printf
		if (Prompt 255 Readline)
			0 Exit
		end

		if (Prompt strlen 0 ==)
			continue
		end

		auto nt
		Prompt PName ' ' 255 strntok nt!

		if (PName strlen 0 ==)
			continue
		end

		auto abuf
		nt@ strlen 1 + Malloc abuf!

		auto argcn
		0 argcn!

		auto p
		abuf@ p!

		auto q
		Argvt q!

		while (nt@ 0 ~=)
			if (argcn@ 32 >=)
				break
			end

			nt@ PComp ' ' 255 strntok nt!

			p@ PComp strcpy

			p@ q@ !

			PComp strlen 1 + p +=

			1 argcn +=
			4 q +=
		end

		auto pid
		argcn@ Argvt PName VSpawn pid!

		abuf@ Free

		if (pid@ iserr)
			pid@ abs pid!
			[pid@]ErrorNames@ PName "sh: couldn't spawn %s: %s\n" Printf
		end else
			auto pret

			Wait pret! pid!
		end
	end
end