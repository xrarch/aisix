#include "<df>/dragonfruit.h"
#include "<ll>/rtaisix/aisix.h"

buffer Prompt 256
buffer PName 256
buffer PComp 256
buffer Argvt (32 4 *)

var LinePoint 0

var Jobs 0

fn String { m -- word }
	256 Calloc word!

	auto q
	word@ q!

	auto chars
	0 chars!

	1 LinePoint +=

	while (1)
		auto char
		LinePoint@ gb char!

		if (char@ 0 ==)
			"sh: unexpected end of line while looking for end of string\n" STDERR FPrintf
			word@ Free
			-1 word!
			return
		end

		if (char@ '\\' ==)
			1 LinePoint +=
			LinePoint@ gb char!

			if (char@ 0 ==)
				"sh: unexpected end of line while looking for end of string A\n" STDERR FPrintf
				word@ Free
				-1 word!
				return
			end
		end elseif (char@ m@ ==)
			1 LinePoint +=
			return
		end

		if (chars@ 255 <)
			char@ q@ sb
			1 q +=
			1 chars +=
		end

		1 LinePoint +=
	end
end

fn Word { -- word }
	if (LinePoint@ ~~)
		0 word!
		return
	end

	while (LinePoint@ gb ' ' ==)
		1 LinePoint +=
	end

	if (LinePoint@ gb 0 ==)
		0 word!
		return
	end

	if (LinePoint@ gb '"' == LinePoint@ gb 0x27 == ||)
		LinePoint@ gb String word!
		return
	end

	256 Calloc word!

	auto q
	word@ q!

	auto i
	0 i!

	auto chars
	0 chars!

	while (1)
		auto char
		LinePoint@ gb char!

		auto term
		0 term!

		if (char@ '"' ==)
			1 term!
		end elseif (char@ 0x27 ==)
			1 term!
		end elseif (char@ ' ' ==)
			1 term!
		end elseif (char@ 0 ==)
			0 LinePoint!
			1 term!
		end elseif (chars@ 0 >)
			if (char@ ';' ==)
				1 term!
			end elseif (char@ '|' ==)
				1 term!
			end elseif (char@ '>' ==)
				if (chars@ 1 > word@ gb '>' ~= ||)
					1 term!
				end
			end elseif (char@ '<' ==)
				1 term!
			end elseif (char@ '&' ==)
				if (chars@ 1 > word@ gb '&' ~= ||)
					1 term!
				end
			end
		end

		if (term@)
			return
		end

		if (chars@ 255 <)
			char@ q@ sb
			1 q +=
			1 chars +=
		end

		1 LinePoint +=

		if (char@ ';' ==)
			return
		end elseif (char@ '|' ==)
			return
		end elseif (char@ '>' ==)
			if (LinePoint@ gb '>' ~=)
				return
			end
		end elseif (char@ '<' ==)
			return
		end elseif (char@ '&' ==)
			if (LinePoint@ gb '&' ~=)
				return
			end
		end
	end
end

table UMaskNames
	""
	"x"
	"w"
	"wx"
	"r"
	"rx"
	"rw"
	"rwx"
endtable

fn Builtin { argcn cmd -- builtin }
	0 builtin!

	auto ok

	auto q

	auto w

	if (cmd@ "cd" strcmp)
		1 builtin!

		if (argcn@ 1 <)
			return
		end

		Argvt@ w!

		w@ Chdir ok!

		if (ok@ iserr)
			ok@ abs ok!
			[ok@]ErrorNames@ w@ "sh: %s: %s\n" STDERR FPrintf
		end
	end elseif (cmd@ "umask" strcmp)
		1 builtin!

		0 q!

		if (argcn@ 1 <)
			1 q!
		end else
			Argvt@ w!

			if (w@ "-S" strcmp)
				2 q!
			end
		end

		if (q@)
			0 UMask ok!
			ok@ UMask drop
			
			if (q@ 1 ==)
				ok@ "%d\n" Printf
			end else
				[ok@ 6 >> 7 & 7 ^]UMaskNames@ "u=%s," Printf
				[ok@ 3 >> 7 & 7 ^]UMaskNames@ "g=%s," Printf
				[ok@ 7 & 7 ^]UMaskNames@ "o=%s\n" Printf
			end

			return
		end

		w@ atoi UMask drop
	end
end

fn IsPath { cand -- ispath }
	if (cand@ "." strcmp)
		1 ispath!
		return
	end

	if (cand@ ".." strcmp)
		1 ispath!
		return
	end

	0 ispath!

	while (cand@ gb)
		if (cand@ gb '/' ==)
			1 ispath!
			return
		end

		1 cand +=
	end
end

const FTRUNC 1
const FAPPEND 2

fn Command { -- done }
	auto pname
	Word pname!

	0 done!

	if (pname@ ~~ pname@ -1 == ||)
		1 done!
		return
	end

	auto arg

	auto fd

	auto argcn
	0 argcn!

	auto waitfor
	1 waitfor!

	auto red
	0 red!

	auto fname
	0 fname!

	auto effect
	0 effect!

	auto q
	Argvt q!

	while (1)
		Word arg!

		if (arg@ ~~)
			1 done!
			break
		end elseif (arg@ -1 ==)
			-1 done!
			break
		end

		if (arg@ ";" strcmp)
			arg@ Free
			break
		end elseif (arg@ "&&" strcmp)
			arg@ Free
			break
		end elseif (arg@ "&" strcmp)
			0 waitfor!
			arg@ Free
			break
		end

		if (red@)
			arg@ Free
			"sh: syntax error\n" STDERR FPrintf
			-1 done!
			break
		end

		if (arg@ ">" strcmp)
			1 red!
		end elseif (arg@ ">>" strcmp)
			1 red!
		end

		if (red@)
			Word fname!

			if (fname@ ~~ fname@ -1 == ||)
				arg@ Free
				"sh: syntax error\n" STDERR FPrintf
				-1 done!
				break
			end

			if (arg@ 1 + gb '>' ==)
				FAPPEND effect!
			end else
				FTRUNC effect!
			end

			arg@ Free

			continue
		end

		if (argcn@ 32 <)
			arg@ q@ !
			4 q +=
			1 argcn +=
		end
	end

	auto ename
	0 ename!
	
	auto nq
	0 nq!

	0 red!

	if (done@ -1 ~=)
		pname@ ename!

		if (pname@ IsPath ~~)
			argcn@ pname@ Builtin red!
			
			if (red@ ~~) (* prepend /bin/ *)
				pname@ strlen 6 + Malloc nq!

				nq@ "/bin/" strcpy

				nq@ 5 + pname@ strcpy

				nq@ pname!
			end
		end

		if (red@ ~~)
			auto pid

			if (effect@ 0 ==)
				argcn@ Argvt pname@ VSpawn pid!
			end else
				if (effect@ FTRUNC ==)
					fname@ O_WRITE O_TRUNC | 438 Create fd!
				end elseif (effect@ FAPPEND ==)
					fname@ O_WRITE O_APPEND | 438 Create fd!
				end

				if (fd@ iserr)
					0 pid!
					fd@ abs fd!
					[fd@]ErrorNames@ fname@ "sh: %s: %s\n" STDERR FPrintf
				end else
					STDIN fd@ STDERR argcn@ Argvt pname@ ASpawn pid!

					fd@ Close drop
				end 
			end

			if (pid@ iserr)
				pid@ abs pid!
				[pid@]ErrorNames@ ename@ "sh: %s: %s\n" STDERR FPrintf
			end elseif (pid@)
				if (waitfor@)
					auto wpid
					-1 wpid!

					while (wpid@ pid@ ~=)
						Wait drop wpid!

						if (wpid@ pid@ ~=)
							wpid@ "sh: done: %d\n" STDERR FPrintf
						end
					end
				end else
					1 Jobs +=

					pid@ Jobs@ "sh: [%d] %d\n" STDERR FPrintf
				end
			end
		end
	end else
		pname@ Free
		1 done!
	end

	if (nq@)
		nq@ Free
	end

	if (ename@)
		ename@ Free
	end

	if (fname@)
		fname@ Free
	end

	0 nq!

	Argvt q!

	while (nq@ argcn@ <)
		q@@ Free

		4 q +=
		1 nq +=
	end
end

fn CommandLine { -- }
	while (Command ~~) end
end

fn Main { ... -- ret }
	TTYI_IGN TTYI_CHILD_ALL | SetTTYIgnore drop

	0 ret!

	auto prmpt
	if (GetUID 0 ==)
		"# " prmpt!
	end else
		"$ " prmpt!
	end
	
	while (1)
		prmpt@ Puts
		if (Prompt 255 Readline)
			0 Exit
		end

		if (Prompt strlen 0 ==)
			continue
		end

		Prompt LinePoint!

		CommandLine
	end
end

fn OldMain { ... -- ret }
	TTYI_IGN TTYI_CHILD_ALL | SetTTYIgnore drop

	0 ret!
	
	while (1)
		"# " Printf
		if (Prompt 255 Readline)
			0 Exit
		end

		if (Prompt strlen 0 ==)
			continue
		end

		Prompt LinePoint!

		auto nt
		Prompt PName ' ' 255 strntok nt!

		if (PName strlen 0 ==)
			continue
		end

		auto abuf
		nt@ strlen 1 + Malloc abuf!

		auto argcn
		0 argcn!

		auto p
		abuf@ p!

		auto q
		Argvt q!

		while (nt@ 0 ~=)
			if (argcn@ 32 >=)
				break
			end

			nt@ PComp ' ' 255 strntok nt!

			p@ PComp strcpy

			p@ q@ !

			PComp strlen 1 + p +=

			1 argcn +=
			4 q +=
		end

		auto pid
		argcn@ Argvt PName VSpawn pid!

		abuf@ Free

		if (pid@ iserr)
			pid@ abs pid!
			[pid@]ErrorNames@ PName "sh: couldn't spawn %s: %s\n" STDERR FPrintf
		end else
			auto pret

			Wait pret! pid!
		end
	end
end