#include "<df>/dragonfruit.h"
#include "<ll>/rta3x/a3x.h"
#include "<inc>/kernel.h"
#include "<inc>/dev/citron.h"

var ClockFound 0

var ClockCmdPort 0
var ClockDataPort 0

const CLOCKINTERVAL 100

fn DrvClockFoundNode { node -- }
	if (ClockFound@)
		return
	end

	1 ClockFound!

	"clock,cmdPort" a3xDGetProperty ClockCmdPort!

	if (ClockCmdPort@ 0 ==)
		return
	end

	"clock,dataPort" a3xDGetProperty ClockDataPort!

	auto intn
	"interrupt#" a3xDGetProperty intn!

	intn@ ClockDataPort@ ClockCmdPort@ "citron:%x,%x int:%d" Printf

	pointerof ClockIntr intn@ InterruptRegister

	ClockSet
end

fn ClockSet { -- }
	auto rs
	InterruptDisable rs!

	CLOCKINTERVAL ClockDataPort@ DCitronOutl
	1 ClockCmdPort@ DCitronCommand

	rs@ InterruptRestore
end

fn ClockIntr { intn -- }
	ClockSet

	intn@ InterruptAck@ InterruptAcker

	CLOCKINTERVAL TimerTick
end