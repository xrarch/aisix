#include "<df>/dragonfruit.h"
#include "<ll>/rta3x/a3x.h"
#include "<inc>/kernel.h"
#include "<inc>/dev/citron.h"

var ClockFound 0

var ClockCmdPort 0
var ClockDataPort 0

const CLOCKINTERVAL 200

fn DrvClockFoundNode { node -- }
	if (ClockFound@)
		return
	end

	1 ClockFound!

	"clock,cmdPort" a3xDGetProperty ClockCmdPort!

	if (ClockCmdPort@ 0 ==)
		return
	end

	"clock,dataPort" a3xDGetProperty ClockDataPort!

	auto intn
	"interrupt#" a3xDGetProperty intn!

	intn@ ClockDataPort@ ClockCmdPort@ "citron:%x,%x irq:%d" BootPrintf

	pointerof ClockIntr intn@ InterruptRegister

	ClockSet
end

fn ClockEpoch { -- sec ms }
	auto rs
	InterruptDisable rs!

	2 ClockCmdPort@ DCitronCommand
	ClockDataPort@ DCitronInl sec!

	3 ClockCmdPort@ DCitronCommand
	ClockDataPort@ DCitronInl ms!

	rs@ InterruptRestore
end

fn ClockSet { -- }
	auto rs
	InterruptDisable rs!

	CLOCKINTERVAL ClockDataPort@ DCitronOutl
	1 ClockCmdPort@ DCitronCommand

	rs@ InterruptRestore
end

externptr Reschedule

fn ClockIntr { intn -- }
	intn@ InterruptAck@ InterruptAcker

	CLOCKINTERVAL TimerTick

	1 Reschedule!
end