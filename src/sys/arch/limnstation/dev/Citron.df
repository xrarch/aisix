#include "<df>/dragonfruit.h"
#include "<ll>/rta3x/a3x.h"
#include "<inc>/kernel.h"

var DCitronBase 0

fn DrvCitronFoundNode { node -- }
	"address" a3xDGetProperty DCitronBase!

	DCitronBase@ "  citron @ 0x%x\n" Printf
end

fn DCitronInb { port -- byte }
	port@ 4 * DCitronBase@ + gb byte!
end

fn DCitronIni { port -- int }
	port@ 4 * DCitronBase@ + gi int!
end

fn DCitronInl { port -- long }
	port@ 4 * DCitronBase@ + @ long!
end

fn DCitronOutb { byte port -- }
	byte@ port@ 4 * DCitronBase@ + sb
end

fn DCitronOuti { int port -- }
	int@ port@ 4 * DCitronBase@ + si
end

fn DCitronOutl { long port -- }
	long@ port@ 4 * DCitronBase@ + !
end

fn DCitronCommand { command port -- }
	port@ 4 * DCitronBase@ + port!

	while (port@ gb 0 ~=) end

	command@ port@ sb

	while (port@ gb 0 ~=) end
end

(* doesn't wait for the device to report the operation as completed before returning *)
fn DCitronCommandASync { command port -- }
	port@ 4 * DCitronBase@ + port!

	while (port@ gb 0 ~=) end

	command@ port@ sb
end