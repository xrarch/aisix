#include "<df>/dragonfruit.h"
#include "<ll>/rtaisix/aisix.h"

buffer MOTDBuf 4096

fn Main { ... -- ret }
	(* kernel process is pid 1 on AISIX, which makes init pid 2 *)
	if (GetPID 2 ~=)
		"init: must be first process!\n" STDERR FPrintf
		1 Exit
	end

	(* don't die when ^C hit on /dev/cons *)
	TTYI_IGN SetTTYIgnore drop

	18 UMask drop

	auto stdio
	"/dev/cons" O_READ Open stdio!
	if (stdio@ iserr)
		1 Exit
	end

	"/dev/cons" O_WRITE Open stdio!
	if (stdio@ iserr)
		1 Exit
	end

	"/dev/cons" O_WRITE Open stdio!
	if (stdio@ iserr)
		1 Exit
	end

	(* /bin/login should do the motd but that's not a thing at the time of writing this *)
	auto motd
	"/sys/motd.txt" O_READ Open motd!
	if (motd@ iserr ~~)
		auto motdsz

		MOTDBuf 4096 motd@ Read motdsz!

		if (motdsz@ iserr)
			motdsz@ abs motdsz!
			[motdsz@]ErrorNames@ "init: couldn't read /sys/motd.txt: %s\n" Printf
		end else
			MOTDBuf motdsz@ STDOUT Write motdsz!
		end

		motd@ Close drop
	end


	while (1)
		auto shpid
		"/bin/sh" Spawn shpid!

		if (shpid@ iserr)
			shpid@ abs shpid!
			[shpid@]ErrorNames@ "init: couldn't spawn /bin/sh: %s\n" Abort
		end

		auto pid

		while (1)
			Wait ret! pid!

			if (pid@ shpid@ ==)
				break
			end
		end
	end
end