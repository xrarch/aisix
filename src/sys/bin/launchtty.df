#include "<df>/dragonfruit.h"
#include "<ll>/rtaisix/aisix.h"

buffer TTYInfoBuf TTYInfo_SIZEOF

fn Main { ... -- ret }
	0 ret!

	if (argc@ 2 <)
		return
	end

	auto tty
	[1]argv@ tty!

	auto stdio
	tty@ O_READ Open stdio!
	if (stdio@ STDIN ~=)
		1 Exit
	end

	tty@ O_WRITE Open stdio!
	if (stdio@ STDOUT ~=)
		1 Exit
	end

	tty@ O_WRITE Open stdio!
	if (stdio@ STDERR ~=)
		1 Exit
	end

	if (stdio@ 0 FChown iserr)
		tty@ "launchtty: couldn't chown %s for root\n" Abort
	end

	TTY_IOCTL_INFO TTYInfoBuf 0 0 STDOUT IOCtl ret!

	if (ret@ iserr)
		ret@ abs ret!
		[ret@]ErrorNames@ "launchtty: couldn't get tty info: %s\n" Abort
	end

	0 TTYInfoBuf TTYInfo_Mode + !

	TTY_IOCTL_SET TTYInfoBuf 0 0 STDOUT IOCtl ret!

	if (ret@ iserr)
		ret@ abs ret!
		[ret@]ErrorNames@ "launchtty: couldn't reset tty: %s\n" Abort
	end

	'\n' Putc

	auto lpid
	"/sys/bin/consolelogin" Spawn lpid!

	if (lpid@ iserr)
		lpid@ abs lpid!
		[lpid@]ErrorNames@ "launchtty: couldn't spawn /sys/bin/consolelogin: %s\n" Abort
	end

	while (1)
		auto pid
		Wait ret! pid!

		if (pid@ lpid@ ==)
			break
		end
	end

	if (ret@ 143 ==) (* it was nuked by the user, line does not need to be terminated *)
		0 Exit
	end else
		ret@ Exit
	end
end