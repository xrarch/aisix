#include "<df>/dragonfruit.h"
#include "<ll>/rtaisix/aisix.h"

fn Main { ... -- ret }
	0 ret!

	if (argc@ 2 <)
		return
	end

	auto tty
	[1]argv@ tty!

	auto stdio
	tty@ O_READ Open stdio!
	if (stdio@ 0 ~=)
		1 Exit
	end

	tty@ O_WRITE Open stdio!
	if (stdio@ 1 ~=)
		1 Exit
	end

	tty@ O_WRITE Open stdio!
	if (stdio@ 2 ~=)
		1 Exit
	end

	if (stdio@ 0 FChown iserr)
		1 Exit
	end

	'\n' Putc

	auto lpid
	"/sys/bin/consolelogin" Spawn lpid!

	if (lpid@ iserr)
		lpid@ abs lpid!
		[lpid@]ErrorNames@ "launchtty: couldn't spawn /sys/bin/consolelogin: %s\n" Abort
	end

	while (1)
		auto pid
		Wait ret! pid!

		if (pid@ lpid@ ==)
			break
		end
	end

	if (ret@ 143 ==) (* it was nuked by the user, line does not need to be terminated *)
		0 Exit
	end else
		ret@ Exit
	end
end