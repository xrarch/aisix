#include "<df>/dragonfruit.h"
#include "<inc>/kernel.h"

fn InitMutex { name mutex -- }
	0 mutex@ Mutex_Locked + !
	0 mutex@ Mutex_OwnerThread + !
	name@ mutex@ Mutex_Name + !
	mutex@ mutex@ Mutex_LockQ + EventQueue_Mutex + !
end

fn LockMutex { mutex -- killed }
	if (CurrentThread@ Thread_Killed + @)
		1 killed!
		return
	end

	auto rs
	InterruptDisable rs!

	0 killed!

	while (mutex@ Mutex_Locked + @)
		if (CurrentThread@ 0 ==)
			"LockMutex: if we aren't in thread context, we can lock a mutex, but we can't wait on one!\n" Panic
		end

		if (mutex@ Mutex_OwnerThread + @ CurrentThread@ ==)
			auto mn

			"unknown" mn!

			if (mutex@ Mutex_Name + @)
				mutex@ Mutex_Name + @ mn!
			end

			mn@ "LockMutex: '%s' already owned\n" Panic
		end

		if (mutex@ Mutex_LockQ + WaitQueue)
			1 killed!
			rs@ InterruptRestore
			return
		end
	end

	1 mutex@ Mutex_Locked + !
	CurrentThread@ mutex@ Mutex_OwnerThread + !

	rs@ InterruptRestore
end

fn UnlockMutex { mutex -- }
	auto rs
	InterruptDisable rs!

	if (mutex@ Mutex_OwnerThread + @ CurrentThread@ ~=)
		auto mn

		"unknown" mn!

		if (mutex@ Mutex_Name + @)
			mutex@ Mutex_Name + @ mn!
		end

		mutex@ Mutex_OwnerThread + @ mn@ "UnlockMutex: never owned '%s', was owned by %x\n" Panic
	end

	0 mutex@ Mutex_Locked + !
	0 mutex@ Mutex_OwnerThread + !

	mutex@ Mutex_LockQ + WakeQueue

	rs@ InterruptRestore
end