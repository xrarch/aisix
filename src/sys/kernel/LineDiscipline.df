(* generic code for tty, ptty, syscon, etc; anything that wants line discipline *)

#include "<df>/dragonfruit.h"
#include "<inc>/kernel.h"

fn AllocTTY { ibuf obuf -- tty }
	TTY_SIZEOF Calloc tty!

	if (tty@ iserr)
		-ENOMEM tty!
		return
	end

	ibuf@ tty@ TTY_IBuffer + !

	pointerof TTYInput ibuf@ IOBuffer_InputF + !

	tty@ ibuf@ IOBuffer_TTY + !

	obuf@ tty@ TTY_OBuffer + !
end

const MAXPRINT 0x7E

table TTYCtrl
	'@'
	'A' 'B' 'C'
	'D' 'E' 'F'
	'G' 'H' 'I'
	'J' 'K' 'L'
	'M' 'N' 'O'
	'P' 'Q' 'R'
	'S' 'T' 'U'
	'V' 'W' 'X'
	'Y' 'Z'
	'['
	'\\'
	']'
	'^'
	'_'
	' '
endtable

fn TTYRubout { ib ob -- ro }
	auto c
	ib@ IOBufRubout c!

	if (c@ ERR ==)
		0 ro!
		return
	end

	if (c@ '\n' ==)
		0 ro!
		c@ ib@ 0 IOBufPutc drop
		return
	end

	if (c@ 4 == c@ 3 == ||)
		0 ro!
		c@ ib@ 0 IOBufPutc drop
		return
	end

	1 ro!

	if (c@ MAXPRINT >)
		return
	end

	auto rt
	1 rt!

	if (c@ 0x20 <)
		2 rt!
	end

	while (rt@)
		'\b' ob@ 0 IOBufPutc drop
		' ' ob@ 0 IOBufPutc drop
		'\b' ob@ 0 IOBufPutc drop

		1 rt -=
	end
end

externptr TTYKill

(* this is often called from interrupt context,
so NEVER sleep on any IOBufPutc's *)
fn (IOCharInput) TTYInput { c buf -- }
	auto tty
	buf@ IOBuffer_TTY + @ tty!

	auto ob
	tty@ TTY_OBuffer + @ ob!

	if (ob@ 0 ==)
		return
	end

	if (c@ '\b' ==)
		buf@ ob@ TTYRubout drop
	end elseif (c@ 21 ==) (* ^U *)
		while (buf@ ob@ TTYRubout) end
	end else
		if (c@ buf@ 0 IOBufPutc 1 ==)
			if (c@ MAXPRINT >)
				return
			end

			if (c@ '\n' ==)
				c@ ob@ 0 IOBufPutc drop
				return
			end

			if (c@ 0x20 <)
				'^' ob@ 0 IOBufPutc drop
				[c@]TTYCtrl@ ob@ 0 IOBufPutc drop

				if (c@ 3 ==)
					(* defer to scheduler to avoid killing the process we might have interrupted *)
					(* this should work because a human probably wont be typing ^C faster than the
					scheduling frequency but you never know i guess *)
					tty@ TTYKill!
				end
			end else
				c@ ob@ 0 IOBufPutc drop
			end
		end
	end
end

fn TTYRead { pm buf len tty -- bytes }
	auto ib
	tty@ TTY_IBuffer + @ ib!

	if (ib@ ~~)
		-ENOTBLK bytes!
		return
	end

	auto pa
	auto r
	if (buf@ 4095 &)
		pm@ buf@ VMWalk pa! r!

		if (r@ iserr)
			r@ bytes!
			return
		end
	end

	auto rs

	0 bytes!

	while (bytes@ len@ <)
		InterruptDisable rs!

		auto c
		ib@ 1 IOBufGetc c!

		if (c@ iserr)
			rs@ InterruptRestore
			c@ bytes!
			return
		end

		if (c@ 4 ==) (* ^D *)
			if (bytes@ 0 ==)
				rs@ InterruptRestore
				return
			end

			4 ib@ IOBufPutBack

			rs@ InterruptRestore
			return
		end

		rs@ InterruptRestore

		if (c@ 3 ==) (* ^C *)
			continue
		end

		if (buf@ 4095 & 0 ==)
			pm@ buf@ VMWalk pa! r!

			if (r@ iserr)
				r@ bytes!
				return
			end
		end

		c@ pa@ sb
		1 pa +=
		1 buf +=
		1 bytes +=

		if (c@ '\n' ==)
			return
		end
	end
end

fn TTYWrite { pm buf len tty -- bytes }
	auto ob
	tty@ TTY_OBuffer + @ ob!

	if (ob@ ~~)
		-ENOTBLK bytes!
		return
	end

	0 bytes!

	auto pa
	auto r
	if (buf@ 4095 &)
		pm@ buf@ VMWalk pa! r!

		if (r@ iserr)
			r@ bytes!
			return
		end
	end

	while (bytes@ len@ <)
		if (buf@ 4095 & 0 ==)
			pm@ buf@ VMWalk pa! r!

			if (r@ iserr)
				r@ bytes!
				return
			end
		end

		pa@ gb ob@ 1 IOBufPutc r!

		if (r@ iserr)
			r@ bytes!
			return
		end

		1 pa +=
		1 buf +=
		1 bytes +=
	end
end