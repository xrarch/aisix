#include "<df>/dragonfruit.h"
#include "<inc>/kernel.h"

const Filesystems 2

externptr AISIXFSTab

table FilesystemTab
	pointerof DevFSTab
	pointerof AISIXFSTab
endtable

var MountListHead 0

var Mounted 0

var DevMount 0

var RootVNode 0
public RootVNode

buffer MountListMutex Mutex_SIZEOF

fn LockMountList { -- killed }
	MountListMutex LockMutex killed!
end

fn UnlockMountList { -- }
	MountListMutex UnlockMutex
end

fn MountRef { mount -- }
	auto rs
	InterruptDisable rs!

	1 mount@ Mount_MRefs + +=

	rs@ InterruptRestore
end

fn MountUnref { mount -- }
	auto rs
	InterruptDisable rs!

	if (mount@ Mount_MRefs + @ 0 ==)
		"MountUnref\n" Panic
	end

	1 mount@ Mount_MRefs + -=

	rs@ InterruptRestore
end

fn VFSInit { -- }
	"mount list" MountListMutex InitMutex

	auto syncw
	pointerof VFSSyncWorker "sync" KernelThreadNew syncw!

	if (syncw@ iserr)
		syncw@ abs syncw!
		[syncw@]ErrorNames@ "couldn't start sync worker: %s\n" Panic
	end

	syncw@ ThreadReady
end

fn VFSMountRoot { -- }
	auto rd
	auto rdt
	"root" ArgsValue rdt!

	if (rdt@)
		rdt@ DevByName rd!
		rdt@ Free
	end

	if (rd@ ~~)
		RootDevice@ rd!
	end

	if (rd@ ~~)
		"couldn't find root device!\n" Panic
	end

	auto rootmount

	auto dp
	128 Malloc dp!

	if (dp@ iserr)
		"oh no\n" Panic
	end

	dp@ "/dev/" strcpy

	dp@ 5 + rd@ Device_Name + strcpy

	0 0 "aisixfs" FSByName rd@ dp@ VFSMount rootmount!

	dp@ Free

	if (rootmount@ iserr)
		rootmount@ abs rootmount!
		[rootmount@]ErrorNames@ "couldn't mount root: %s\n" Panic
	end

	dp@ "mounted %s on / as aisixfs\n" Printf

	rootmount@ Mount_Root + @ RootVNode!

	RootVNode@ VNodeRef
	RootVNode@ KernelProcess@ Process_CWDVNode + !
	KernelProcess@ Process_CWDPathString + "/" CWDPATHLEN 1 - strncpy

	0 "/dev" "devfs" FSByName 0 "devfs" VFSMount DevMount!

	if (DevMount@ iserr)
		DevMount@ abs DevMount!
		[DevMount@]ErrorNames@ "couldn't mount devfs: %s\n" Panic
	end

	"mounted devfs on /dev\n" Printf
end

fn VRead { pm buf len seek vnode -- bytes }
	pm@ buf@ len@ seek@ vnode@ vnode@ VNode_Mount + @ Mount_Filesystem + @ Filesystem_Read + @ FSRead bytes!
end

fn VWrite { pm buf len seek vnode -- bytes }
	pm@ buf@ len@ seek@ vnode@ vnode@ VNode_Mount + @ Mount_Filesystem + @ Filesystem_Write + @ FSWrite bytes!
end

fn VTrunc { vnode -- ok }
	vnode@ vnode@ VNode_Mount + @ Mount_Filesystem + @ Filesystem_Trunc + @ FSTrunc ok!
end

const CANONLEN 1024

fn VFSCanonicalizePath { path -- canon }
	CANONLEN Calloc canon!

	if (canon@ iserr)
		return
	end

	auto pcomp
	256 Calloc pcomp!

	if (pcomp@ iserr)
		canon@ Free
		pcomp@ canon!
		return
	end

	if (path@ gb '/' ==)
		'/' canon@ sb
	end else
		canon@ CurrentThread@ Thread_Process + @ Process_CWDPathString + CANONLEN 1 - strncpy
	end

	auto ci
	canon@ strlen ci!

	auto lenleft
	CANONLEN ci@ - lenleft!

	while (1)
		while (path@ gb '/' ==)
			1 path +=
		end

		if (path@ gb 0 ==)
			break
		end

		auto pr
		0 pr!

		while (1)
			if (path@ gb '/' == path@ gb 0 == ||)
				break
			end

			if (pr@ 255 <)
				path@ gb pcomp@ pr@ + sb
				1 pr +=
			end

			1 path +=
		end

		0 pcomp@ pr@ + sb

		if (pcomp@ "." strcmp)
			continue
		end

		if (pcomp@ ".." strcmp)
			while (canon@ ci@ + gb '/' ~=)
				if (ci@ 0 ==)
					"VFSCanonicalizePath\n" Panic
				end

				1 ci -=
				1 lenleft +=
			end

			if (ci@ 0 ==)
				1 ci!
			end

			canon@ ci@ + strzero

			continue
		end

		if (ci@ 1 >)
			'/' canon@ ci@ + sb
			1 ci +=
			1 lenleft -=
		end

		canon@ ci@ + pcomp@ lenleft@ 1 - strncpy
		pcomp@ strlen pr!
		pr@ lenleft@ min ci +=
		pr@ lenleft@ min lenleft -=
	end

	pcomp@ Free
end

fn VFSPath { path -- vnode }
	auto name
	auto dirvnode
	path@ VFSPathX name! vnode! dirvnode! drop

	if (name@)
		name@ Free
	end

	if (dirvnode@)
		dirvnode@ VNodePut
	end
end

fn VFSPathX { path -- sook dirvnode vnode name }
	0 name!
	0 dirvnode!

	if (path@ gb 0 ==)
		-ENOENT vnode!
		return
	end elseif (path@ gb '/' ==)
		RootVNode@ vnode!
	end else
		CurrentThread@ Thread_Process + @ Process_CWDVNode + @ vnode!
	end

	auto pcomp
	256 Malloc pcomp!

	if (pcomp@ iserr)
		pcomp@ vnode!
		return
	end

	if (vnode@ VNodeLock)
		-EINTR vnode!
		return
	end

	vnode@ VNodeRef

	auto pr

	auto dirent
	VDirent_SIZEOF Calloc dirent!

	auto mount
	vnode@ VNode_Mount + @ mount!

	auto fs
	mount@ Mount_Filesystem + @ fs!

	auto uid
	CurrentThread@ Thread_Process + @ Process_EUID + @ uid!

	auto ok

	while (1)
		0 sook!

		vnode@ VNode_Type + @ ok!

		if (path@ gb '/' ==)
			if (ok@ VNODE_DIR ~=)
				vnode@ VNodePut
				pcomp@ Free
				if (dirent@ VDirent_Cookie + @)
					dirent@ VDirent_Cookie + @ Free
				end
				dirent@ Free
				-ENOTDIR vnode!
				return
			end

			while (path@ gb '/' ==)
				1 path +=
			end
		end

		if (path@ gb 0 ==)
			1 sook!
			(* finished *)
			pcomp@ name!
			if (dirent@ VDirent_Cookie + @)
				dirent@ VDirent_Cookie + @ Free
			end
			dirent@ Free
			return
		end

		vnode@ WORLD_X uid@ VForbidden pr!

		if (pr@ iserr)
			vnode@ VNodePut
			pcomp@ Free
			if (dirent@ VDirent_Cookie + @)
				dirent@ VDirent_Cookie + @ Free
			end
			dirent@ Free
			pr@ vnode!
			return
		end

		0 pr!

		while (1)
			if (path@ gb '/' == path@ gb 0 == ||)
				break
			end

			if (pr@ 255 <)
				path@ gb pcomp@ pr@ + sb
				1 pr +=
			end

			1 path +=
		end

		0 pcomp@ pr@ + sb

		if (pcomp@ "." strcmp)
			continue
		end

		path@ pr!

		while (pr@ gb '/' ==)
			1 pr +=
		end

		if (pr@ gb 0 ==)
			1 sook!
		end

		if (pcomp@ ".." strcmp)
			if (vnode@ VNode_DirParentVNID + @ vnode@ VNode_VNID + @ ==)
				mount@ Mount_Covering + @ ok!

				if (ok@)
					(* step out of the filesystem *)

					if (dirvnode@)
						dirvnode@ VNodePut
					end

					0 dirvnode!

					vnode@ VNodePut

					ok@ VNode_DirParentVNID + @ ok@ VNode_Mount + @ VNodeGet vnode!

					if (vnode@ iserr)
						pcomp@ Free

						if (dirent@ VDirent_Cookie + @)
							dirent@ VDirent_Cookie + @ Free
						end
						dirent@ Free
						return
					end

					vnode@ VNode_Mount + @ mount!
					mount@ Mount_Filesystem + @ fs!
				end

				continue
			end else
				if (dirvnode@)
					dirvnode@ VNodePut
				end

				vnode@ dirvnode!

				vnode@ VNode_DirParentVNID + @ mount@ VNodeGet vnode!

				if (vnode@ iserr)
					pcomp@ Free

					if (dirent@ VDirent_Cookie + @)
						dirent@ VDirent_Cookie + @ Free
					end
					dirent@ Free
					return
				end

				continue
			end
		end

		vnode@ dirent@ VDirent_DirVNode + !

		dirent@ fs@ Filesystem_RewindDir + @ FSRewindDir ok!

		if (ok@ iserr)
			vnode@ VNodePut
			pcomp@ Free
			if (dirent@ VDirent_Cookie + @)
				dirent@ VDirent_Cookie + @ Free
			end
			dirent@ Free
			ok@ vnode!
			return
		end

		while (1)
			dirent@ fs@ Filesystem_ReadDir + @ FSReadDir ok!

			if (ok@ iserr)
				if (dirvnode@)
					dirvnode@ VNodePut
				end

				vnode@ dirvnode!
				pcomp@ name!
				if (dirent@ VDirent_Cookie + @)
					dirent@ VDirent_Cookie + @ Free
				end
				dirent@ Free
				ok@ vnode!
				return
			end

			if (dirent@ VDirent_Name + @ pcomp@ strcmp)
				if (dirvnode@)
					dirvnode@ VNodePut
				end

				vnode@ dirvnode!

				dirent@ VDirent_VNID + @ mount@ VNodeGet ok!

				ok@ vnode!

				if (ok@ iserr)
					pcomp@ Free

					if (dirent@ VDirent_Cookie + @)
						dirent@ VDirent_Cookie + @ Free
					end
					dirent@ Free
					return
				end

				break
			end
		end

		if (vnode@ VNode_CoveredBy + @)
			if (dirvnode@)
				dirvnode@ VNodePut
			end

			0 dirvnode!

			vnode@ VNode_CoveredBy + @ ok!
			vnode@ VNodePut
			ok@ vnode!
			vnode@ VNode_Mount + @ mount!
			mount@ Mount_Filesystem + @ fs!

			if (vnode@ VNodeLock)
				-EINTR vnode!
				pcomp@ Free

				if (dirent@ VDirent_Cookie + @)
					dirent@ VDirent_Cookie + @ Free
				end
				dirent@ Free
				return
			end

			vnode@ VNodeRef
		end
	end
end

fn FSByName { name -- fs }
	auto i
	0 i!

	while (i@ Filesystems <)
		[i@]FilesystemTab@ fs!

		if (fs@ Filesystem_Name + @ name@ strcmp)
			return
		end

		1 i +=
	end

	0 fs!
end

fn SMount { type dir pdev flags -- ok }
	if (CurrentThread@ Thread_Process + @ Process_EUID + @ 0 ~=)
		-EPERM ok!
		return
	end

	type@ FSByName type!

	if (type@ ~~)
		-EINVAL ok!
		return
	end

	auto canon

	if (pdev@)
		pdev@ VFSPath ok!

		if (ok@ iserr)
			return
		end

		if (ok@ VNode_Type + @ VNODE_BLOCK ~=)
			ok@ VNodePut
			-ENOTBLK ok!
			return
		end

		auto dev

		ok@ VNode_FSData + @ dev!

		ok@ VNodePut

		pdev@ ok!

		pdev@ VFSCanonicalizePath pdev!

		if (pdev@ iserr)
			ok@ pdev!
		end else
			1 canon!
		end
	end else
		type@ Filesystem_Name + @ pdev!
		0 canon!
		0 dev!
	end

	flags@ dir@ type@ dev@ pdev@ VFSMount ok!

	if (ok@ iserr)
		return
	end

	0 ok!
end

fn VFSMount { flags path fs dev devpath -- mount }
	if (fs@ 0 ==)
		"VFSMount: fs = 0\n" Panic
	end

	if (LockMountList)
		-EINTR mount!
		return
	end

	auto covervn
	0 covervn!

	if (path@)
		path@ VFSPath covervn!

		if (covervn@ iserr)
			covervn@ mount!

			UnlockMountList
			return
		end

		if (covervn@ VNode_Type + @ VNODE_DIR ~=)
			covervn@ VNodePut

			-ENOTDIR mount!

			UnlockMountList
			return
		end

		if (covervn@ VNode_Refs + @ 1 >)
			covervn@ VNodePut

			-EBUSY mount!

			UnlockMountList
			return
		end
	end else
		if (RootVNode@)
			"mounting root twice?\n" Panic
		end
	end

	Mount_SIZEOF Calloc mount!

	if (mount@ iserr)
		UnlockMountList
		if (covervn@)
			covervn@ VNodePut
		end
		-ENOMEM mount!
		return
	end

	flags@ mount@ Mount_Flags + !

	auto rs

	if (dev@)
		InterruptDisable rs!

		if (dev@ Device_Mount + @ 0 ~=)
			rs@ InterruptRestore
			UnlockMountList
			mount@ Free
			if (covervn@)
				covervn@ VNodePut
			end
			-EBUSY mount!
			return
		end

		mount@ dev@ Device_Mount + !

		rs@ InterruptRestore
	end

	auto fsdata
	auto root

	fs@ mount@ Mount_Filesystem + !

	dev@ mount@ Mount_Device + !

	mount@ fs@ Filesystem_Mount + @ FSMount root!

	if (root@ iserr)
		mount@ Free
		root@ mount!
		0 dev@ Device_Mount + !
		UnlockMountList
		if (covervn@)
			covervn@ VNodePut
		end
		return
	end

	root@ mount@ Mount_Root + !

	covervn@ mount@ Mount_Covering + !

	auto canon

	auto pname

	if (path@)
		1 canon!
		path@ VFSCanonicalizePath pname!
		if (pname@ iserr)
			path@ pname!
			0 canon!
		end
	end else
		0 canon!
		"/" pname!
	end

	mount@ Mount_Path + pname@ MOUNTNAMELEN 1 - strncpy

	if (canon@)
		pname@ Free
	end

	mount@ Mount_DevPath + devpath@ MOUNTNAMELEN 1 - strncpy

	if (covervn@)
		root@ covervn@ VNode_CoveredBy + !

		covervn@ VNodeUnlock
	end

	root@ VNodeUnlock

	if (MountListHead@)
		MountListHead@ mount@ Mount_Next + !
		mount@ MountListHead@ Mount_Prev + !
	end

	mount@ MountListHead!

	1 Mounted +=

	UnlockMountList
end

fn UMount { path -- ok }
	if (CurrentThread@ Thread_Process + @ Process_EUID + @ 0 ~=)
		-EPERM ok!
		return
	end

	auto vnode
	path@ VFSPath vnode!

	if (vnode@ iserr)
		vnode@ ok!
		return
	end

	auto mount
	0 mount!

	if (vnode@ VNode_Type + @ VNODE_BLOCK ==)
		vnode@ VNode_FSData + @ Device_Mount + @ mount!
	end elseif (vnode@ VNode_Mount + @ Mount_Root + @ vnode@ ==)
		vnode@ VNode_Mount + @ mount!
	end

	vnode@ VNodePut

	if (mount@ 0 ==)
		-ENODEV ok!
		return
	end

	mount@ VFSUnmount ok!
end

fn VFSUnmountAll { -- ok }
	auto mount
	auto nmount

	0 ok!

	while (MountListHead@)
		MountListHead@ Mount_Path + "unmounting %s\n" Printf

		MountListHead@ VFSUnmount ok!
		if (ok@ iserr)
			return
		end
	end
end

fn VFSUnmount { mount -- ok }
	auto rs
	InterruptDisable rs!

	if (mount@ Mount_MRefs + @ 0 ~=)
		-EBUSY ok!
		rs@ InterruptRestore
		return
	end

	if (mount@ Mount_VRefs + @ 1 >)
		-EBUSY ok!
		rs@ InterruptRestore
		return
	end

	if (LockMountList)
		rs@ InterruptRestore
		-EINTR ok!
		return
	end

	1 mount@ Mount_Busy + !

	mount@ MountRef

	rs@ InterruptRestore

	auto root
	mount@ Mount_Root + @ root!

	if (root@ VNode_Refs + @ 1 ~=)
		(* should have caught this earlier *)
		root@ VNode_Refs + @ "VFSUnmount: root has %d references\n" Panic
	end

	root@ VNodeUnref

	auto covering

	mount@ Mount_Covering + @ covering!

	if (covering@)
		0 covering@ VNode_CoveredBy + !
		covering@ VNodeUnref
	end

	mount@ 1 SyncVNodes ok!

	if (ok@ iserr)
		ok@ abs ok!
		[ok@]ErrorNames@ "VFSUnmount: couldn't sync: %s\n" Printf
	end
	
	bsync drop

	auto link
	mount@ Mount_Next + @ link!

	if (mount@ MountListHead@ ==)
		link@ MountListHead!
	end

	if (link@)
		mount@ Mount_Prev + @ link@ Mount_Prev + !
	end

	mount@ Mount_Prev + @ link!

	if (link@)
		mount@ Mount_Next + @ link@ Mount_Next + !
	end

	UnlockMountList

	auto fs
	mount@ Mount_Filesystem + @ fs!

	mount@ fs@ Filesystem_Unmount + @ FSUnmount ok!

	if (mount@ Mount_Device + @)
		0 mount@ Mount_Device + @ Device_Mount + !
	end

	mount@ Free
end

fn VForbidden { vnode rwx uid -- ok }
	auto fl
	vnode@ VNode_Mount + @ Mount_Flags + @ fl!

	auto bits
	vnode@ VNode_Permissions + @ bits!

	auto permbits

	if (uid@ 0 ==) (* superuser *)
		7 permbits!
	end else
		auto shift
		if (vnode@ VNode_UID + @ uid@ ==)
			6 shift! (* owner *)
		end else (* world; groups NYI *)
			0 shift!
		end

		bits@ shift@ >> 7 & permbits!

		if (fl@ FS_NOUID &)
			WORLD_W WORLD_R | permbits |=
		end
	end

	0 ok!

	if (permbits@ rwx@ | permbits@ ~=)
		-EACCES ok!
	end

	if (vnode@ VNode_Type + @ VNODE_DIR ~=)
		if (rwx@ WORLD_X & bits@ XMASK & 0 == &&)
			-EACCES ok!
		end
	end

	if (rwx@ WORLD_W &)
		if (fl@ FS_READONLY &)
			-EROFS ok!
		end
	end
end

fn VFSSync { -- ok }
	LockMountList ok!

	if (ok@)
		-EINTR ok!
		return
	end

	auto mount

	MountListHead@ mount!

	while (mount@)
		mount@ 0 SyncVNodes ok!

		if (ok@ iserr)
			ok@ abs ok!
			[ok@]ErrorNames@ mount@ Mount_Path + "kernel: %s: vnode sync error: %s\n" Printf
		end

		mount@ Mount_Next + @ mount!
	end

	UnlockMountList

	bsync ok!
end

fn VFSSyncWorker { -- }
	while (1)
		if (5000 SleepFor)
			"sync thread killed in its sleep\n" Panic
		end

		if (Halting@) (* die if system is shutting down *)
			ThreadExit
		end

		auto ok
		VFSSync ok!

		if (ok@ iserr)
			ok@ abs ok!
			[ok@]ErrorNames@ "kernel: sync error: %s\n" Printf
		end
	end
end

fn VFSPrintMounts { -- }
	auto mount

	MountListHead@ mount!
	while (mount@)
		auto path
		mount@ Mount_Path + path!

		auto devpath
		mount@ Mount_DevPath + devpath!

		auto fs
		mount@ Mount_Filesystem + @ fs!

		fs@ Filesystem_Name + @ path@ devpath@ "%s on %s as %s\n" Printf

		mount@ Mount_Next + @ mount!
	end
end