#include "<df>/dragonfruit.h"
#include "<inc>/kernel.h"

extern PMMInit { -- }
extern HeapInit { -- }
extern PlatformInit { -- }
extern ThreadInit { -- }
extern BlockInit { -- }

extern HeapDump { -- }

extern Scheduler { -- }

extern KLogInit { -- }

extern TriviaTable { ksz -- }

fn AISIX { ksz args -- ret }
	0 ret!

	PMMInit

	HeapInit

	args@ ArgsInit

	KLogInit

	if (TriviaSwitch@ ~~)
		"\[[33mAISIX is preparing your system\[[0m\n" Printf
	end else
		ksz@ TriviaTable
	end

	PlatformInit

	BlockInit

	ThreadInit

	Scheduler

	(* the scheduler loop shouldn't return, but if it does, drop back out here *)
end

extern VFSInit { -- }

extern VFSMountRoot { -- }

extern VCacheInit { -- }

extern VConsInit { -- }

extern SelectConsole { -- }

extern CPUHlt { -- }

var TTYKill 0
public TTYKill

extern KDebug { -- }

fn AISIXInitialThread { -- }
	VCacheInit

	VFSInit

	VConsInit

	SelectConsole

	VFSMountRoot

	auto vnode
	"/sys/bin/init" VFSPath vnode!

	if (vnode@ iserr)
		vnode@ abs vnode!
		[vnode@]ErrorNames@ "couldn't open /sys/bin/init: %s\n" Panic
	end else
		vnode@ VNodeUnlock

		vnode@ "/sys/bin/init" 0 0 0 NP_INHERIT 0 0 VNewProcess InitProcess!

		vnode@ VNodeUnref

		if (InitProcess@ iserr)
			InitProcess@ abs InitProcess!
			[InitProcess@]ErrorNames@ "couldn't spawn /sys/bin/init: %s\n" Panic
		end
	end

	CurrentThread@ IdleThread!

	(* become an idle thread. also tty killer *)
	while (1)
		CPUHlt

		auto kt
		TTYKill@ kt!

		if (kt@)
			0 TTYKill!
			kt@ KillTTY
		end

		Yield
	end
end