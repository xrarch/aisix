#include "<df>/dragonfruit.h"
#include "<inc>/kernel.h"

const BufHeadSize (NBUF Buffer_SIZEOF *)

buffer BufferHeads BufHeadSize

buffer BufCacheMutex Mutex_SIZEOF

buffer BufferHead Buffer_SIZEOF

fn LockBCache { -- }
	BufCacheMutex LockMutexUninterruptible
end

fn UnlockBCache { -- }
	BufCacheMutex UnlockMutex
end

fn LockBuf { buf -- }
	buf@ Buffer_Mutex + LockMutexUninterruptible
end

fn UnlockBuf { buf -- }
	buf@ Buffer_Mutex + UnlockMutex
end

fn BlockInit { -- }
	"block cache" BufCacheMutex InitMutex

	BufferHead BufferHead Buffer_Prev + !
	BufferHead BufferHead Buffer_Next + !

	auto b
	BufferHeads b!

	auto max
	BufferHeads BufHeadSize + max!

	while (b@ max@ <)
		BufferHead Buffer_Next + @ b@ Buffer_Next + !
		BufferHead b@ Buffer_Prev + !

		"block buffer" b@ Buffer_Mutex + InitMutex

		b@ BufferHead Buffer_Next + @ Buffer_Prev + !

		b@ BufferHead Buffer_Next + !

		auto bd
		1 PMMAlloc bd!

		if (bd@ iserr)
			bd@ abs bd!
			[bd@]ErrorNames@ "couldn't allocate block buffer: %s\n" Panic
		end

		bd@ b@ Buffer_Data + !

		Buffer_SIZEOF b +=
	end
end

fn bget { blockno dev -- buf }
	if (CurrentThread@ Thread_Killed + @)
		-EINTR buf!
		return
	end

	LockBCache

	auto rs

	BufferHead Buffer_Next + @ buf!

	auto sbno
	blockno@ sbno!

	auto sdev
	dev@ sdev!

	if (dev@ Device_RawDev + @)
		dev@ Device_RawDev + @ sdev!
		dev@ Device_Offset + @ blockno@ + sbno!
	end

	while (buf@ BufferHead ~=)
		if (buf@ Buffer_RawDev + @ sdev@ ==)
			if (buf@ Buffer_RawBlockNum + @ sbno@ ==)
				1 buf@ Buffer_Refs + +=

				UnlockBCache

				buf@ LockBuf

				return
			end
		end

		buf@ Buffer_Next + @ buf!
	end

	BufferHead Buffer_Prev + @ buf!

	while (buf@ BufferHead ~=)
		if (buf@ Buffer_Refs + @ 0 ==)
			if (buf@ Buffer_Dirty + @)
				auto r
				buf@ bupdate r!

				if (r@ iserr)
					r@ buf!

					UnlockBCache

					return
				end 
			end

			dev@ buf@ Buffer_Device + !
			blockno@ buf@ Buffer_BlockNum + !
			sdev@ buf@ Buffer_RawDev + !
			sbno@ buf@ Buffer_RawBlockNum + !
			0 buf@ Buffer_Valid + !
			1 buf@ Buffer_Refs + !

			UnlockBCache

			buf@ LockBuf

			return
		end

		buf@ Buffer_Prev + @ buf!
	end

	UnlockBCache

	-ENOMEM buf!
end

fn bread { blockno dev -- buf }
	blockno@ dev@ bget buf!

	if (buf@ iserr)
		return
	end

	auto ok

	if (buf@ Buffer_Valid + @ ~~)
		buf@ Buffer_Data + @
		1
		blockno@
		dev@ DeviceRead ok!

		if (ok@ iserr)
			buf@ brelse
			
			ok@ buf!

			return
		end

		1 buf@ Buffer_Valid + !
	end
end

fn bwrite { buf -- }
	1 buf@ Buffer_Dirty + !
end

fn brelse { buf -- }
	1 buf@ Buffer_Refs + -=

	buf@ UnlockBuf

	LockBCache

	if (buf@ Buffer_Refs + @ 0 ==)
		buf@ Buffer_Prev + @ buf@ Buffer_Next + @ Buffer_Prev + !
		buf@ Buffer_Next + @ buf@ Buffer_Prev + @ Buffer_Next + !
		BufferHead Buffer_Next + @ buf@ Buffer_Next + !
		BufferHead buf@ Buffer_Prev + !
		buf@ BufferHead Buffer_Next + @ Buffer_Prev + !
		buf@ BufferHead Buffer_Next + !
	end

	UnlockBCache
end

fn bupdate { buf -- ok }
	buf@ LockBuf

	if (buf@ Buffer_Valid + @ buf@ Buffer_Dirty + @ &&)
		buf@ Buffer_Data + @
		1
		buf@ Buffer_BlockNum + @
		buf@ Buffer_Device + @ DeviceWrite ok!

		0 buf@ Buffer_Dirty + !
	end

	buf@ UnlockBuf
end

fn bsync { -- ok }
	0 ok!

	LockBCache

	auto buf
	BufferHead Buffer_Next + @ buf!

	while (buf@ BufferHead ~=)
		UnlockBCache

		buf@ bupdate ok!

		LockBCache
		buf@ Buffer_Next + @ buf!
	end

	UnlockBCache
end