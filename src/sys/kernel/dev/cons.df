#include "<df>/dragonfruit.h"
#include "<inc>/kernel.h"

var ConsIBuf 0
var ConsOBuf 0

var ConsInited 0

fn SetConsIn { devname -- ok }
	auto dev
	devname@ DevByName dev!

	if (dev@ ~~)
		-ENODEV ok!
		return
	end

	dev@ Device_IBuffer + @ ConsIBuf!

	if (ConsIBuf@ 0 ==)
		-ENOTBLK ok!
		return
	end

	0 ok!
end

fn SetConsOut { devname -- ok }
	auto dev
	devname@ DevByName dev!

	if (dev@ ~~)
		-ENODEV ok!
		return
	end

	dev@ Device_OBuffer + @ ConsOBuf!

	if (ConsOBuf@ 0 ==)
		-ENOTBLK ok!
		return
	end

	0 ok!
end

fn ConsInit { -- }
	if (ConsInited@)
		"ConsInit\n" Panic
	end

	auto ab

	auto indev
	"consin" ArgsValue ab!

	if (ab@)
		ab@ indev!
	end else
		"serial0" indev!
	end

	indev@ "consin=%s\n" Printf

	auto r
	indev@ SetConsIn r!

	if (r@ iserr)
		r@ abs r!
		[r@]ErrorNames@ "couldn't set consin: %s\n" Panic
	end

	if (ab@)
		ab@ Free
	end

	"consout" ArgsValue ab!

	if (ab@)
		ab@ indev!
	end else
		"serial0" indev!
	end

	indev@ "consout=%s\n" Printf

	indev@ SetConsOut r!

	if (r@ iserr)
		r@ abs r!
		[r@]ErrorNames@ "couldn't set consout: %s\n" Panic
	end

	if (ab@)
		ab@ Free
	end

	1 ConsInited!
end