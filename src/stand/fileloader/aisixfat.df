#include "<df>/dragonfruit.h"

externptr TotalRAM
extern IReadBlock { block buf -- }
extern Panic { ... fmt -- }

(* extremely simple, read-only implementation of aisixfat *)

const AFSSuperblockNumber 0x0
const AFSSuperblockCache 0x47F00
const AFSFATCache 0x48F00
const AFSDirCache 0x49F00
const AFSSuperblockMagic 0xAFBBAFBB
const AFSSuperblockVersion 0x5

struct AFSSuperblock
	4 Version
	4 Magic
	4 VolSize
	4 NumFiles
	4 Dirty
	4 BlocksUsed
	4 NumDirs
	4 NumReservedBlocks
	4 FATStart
	4 FATSize
	4 Root
	4 DataStart
	4 RootSize
endstruct

struct AFSDirEnt
	4 type
	4 permissions
	4 uid
	4 reserved
	4 timestamp
	4 startblock
	4 size
	4 bytesize
	32 name
endstruct

table AFSErrors
	"not found"
	"ok"
	"not enough memory"
	"not a file"
endtable
public AFSErrors

fn AFSMount { -- success }
	0 success!

	AFSSuperblockNumber AFSSuperblockCache IReadBlock

	if (AFSSuperblockCache AFSSuperblock_Magic + @ AFSSuperblockMagic ~=)
		"fileloader: invalid superblock\n" Printf
		return
	end

	if (AFSSuperblockCache AFSSuperblock_Version + @ AFSSuperblockVersion ~=)
		"fileloader: bad version on superblock\n" Printf
		return
	end

	1 success!
end

fn AFSLoadFile { count name destptr -- size ok }
	auto entryptr
	name@ AFSFileByName entryptr!
	if (entryptr@ 0 ==)
		0 size! 0 ok! return
	end

	auto cblock

	entryptr@ AFSDirEnt_startblock + @ cblock!
	entryptr@ AFSDirEnt_size + @ size!

	if (count@ -1 ==)
		size@ count!
	end else
		if (count@ 0xFFF & 0 ~=)
			count@ 0xFFFFF000 & 0x1000 + count!
		end

		4096 count /=
	end

	auto nmem
	destptr@ size@ 4096 * + nmem!

	if (nmem@ TotalRAM@ >=)
		size@ 4096 * size! 2 ok! return (* not enough memory *)
	end

	auto i
	0 i!
	while (i@ count@ <)
		cblock@ destptr@ IReadBlock

		cblock@ AFSBlockStatus cblock!

		destptr@ 4096 + destptr!
		1 i +=
	end

	count@ 4096 * size! 1 ok!
end

fn AFSDirentByName { name dirsz dirblock -- dirent }
	if (name@ strlen 0 ==)
		0 dirent!
		return
	end

	auto off
	0 off!
	while (off@ dirsz@ <)
		if (off@ 4096 % 0 ==)
			dirblock@ AFSDirCache IReadBlock
			dirblock@ AFSBlockStatus dirblock!
		end

		auto ptr
		off@ 4096 % AFSDirCache + ptr!

		if (ptr@ AFSDirEnt_type + @ 0 ~=)
			if (ptr@ AFSDirEnt_name + name@ strcmp)
				ptr@ dirent!
				return
			end
		end

		AFSDirEnt_SIZEOF off +=
	end

	0 dirent!
end

fn AFSPathSeek { path -- dirent }
	auto pcomp
	256 Calloc pcomp!

	auto last
	2 last!

	auto lastsz
	AFSSuperblockCache AFSSuperblock_RootSize + @ lastsz!

	auto lastdirblock
	AFSSuperblockCache AFSSuperblock_Root + @ lastdirblock!

	-1 dirent!

	while (path@ 0 ~=)
		path@ pcomp@ '/' 255 strntok path!

		if (pcomp@ strlen 0 ==)
			pcomp@ Free
			if (dirent@ -1 ==) 0 dirent! end
			return
		end

		if (last@ 2 ~=)
			pcomp@ Free
			0 dirent!
			return
		end

		pcomp@ lastsz@ lastdirblock@ AFSDirentByName dirent!
		if (dirent@ 0 ==)
			pcomp@ Free
			0 dirent!
			return
		end

		dirent@ AFSDirEnt_type + @ last!
		dirent@ AFSDirEnt_bytesize + @ lastsz!
		dirent@ AFSDirEnt_startblock + @ lastdirblock!
	end

	pcomp@ Free
end

fn AFSPrintList { path -- }
	auto dirblock
	auto dirsz

	if (path@ strlen 0 ==)
		AFSSuperblockCache AFSSuperblock_RootSize + @ dirsz!
		AFSSuperblockCache AFSSuperblock_Root + @ dirblock!
	end elseif (path@ "/" strcmp)
		AFSSuperblockCache AFSSuperblock_RootSize + @ dirsz!
		AFSSuperblockCache AFSSuperblock_Root + @ dirblock!
	end else
		auto dirent
		path@ AFSPathSeek dirent!
		if (dirent@ 0 ==)
			path@ "couldn't find %s\n" Printf
			return
		end

		if (dirent@ AFSDirEnt_type + @ 2 ~=)
			path@ "%s isn't a directory\n" Printf
			return
		end

		dirent@ AFSDirEnt_startblock + @ dirblock!
		dirent@ AFSDirEnt_bytesize + @ dirsz!
	end

	path@ "%s:\n" Printf

	auto off
	0 off!
	while (off@ dirsz@ <)
		if (off@ 4096 % 0 ==)
			dirblock@ AFSDirCache IReadBlock
			dirblock@ AFSBlockStatus dirblock!
		end

		auto ptr
		off@ 4096 % AFSDirCache + ptr!

		if (ptr@ AFSDirEnt_type + @ 0 ~=)
			if (ptr@ AFSDirEnt_type + @ 1 ==)
				ptr@ AFSDirEnt_bytesize + @ ptr@ AFSDirEnt_name + "\t%s\t%d\n" Printf
			end else
				ptr@ AFSDirEnt_bytesize + @ ptr@ AFSDirEnt_name + "\t%s/\t%d\n" Printf
			end
		end

		AFSDirEnt_SIZEOF off +=
	end

	'\n' Putc
end

fn AFSFileSize { path -- size }
	auto dirent
	path@ AFSFileByName dirent!
	if (dirent@ 0 ==)
		return
	end

	dirent@ AFSDirEnt_bytesize + @ size!
end

fn AFSFileByName { path -- dirent }
	if (path@ strlen 0 ==)
		0 dirent!
		return
	end

	path@ AFSPathSeek dirent!
	if (dirent@ 0 ==)
		return
	end

	if (dirent@ AFSDirEnt_type + @ 1 ~=)
		0 dirent!
		return
	end
end

fn AFSReadRoot { -- }
	AFSSuperblockCache AFSSuperblock_Root + @ AFSDirCache IReadBlock
end

var AFSFatCached -1
fn AFSReadFATBlock { fatblock -- }
	if (fatblock@ AFSFatCached@ ~=) (* only read in new block if not already in cache *)
		fatblock@ AFSSuperblockCache AFSSuperblock_FATStart + @ +
		AFSFATCache IReadBlock
		fatblock@ AFSFatCached!
	end
end

fn AFSBlockStatus { bnum -- status }
	auto fatblock
	auto fatoff

	bnum@ 4096 / fatblock!
	bnum@ 4096 % fatoff!

	fatblock@ AFSReadFATBlock
	fatoff@ 4 * AFSFATCache + @ status!
end