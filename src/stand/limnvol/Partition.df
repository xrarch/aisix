#include "<df>/dragonfruit.h"
#include "<ll>/rta3x/a3x.h"

struct VDB
	16 Label
	128 PartitionTable
	4 Magic
endstruct

struct PTE
	8 Label
	4 Blocks
	4 Status
endstruct

extern PromptYN
extern IWriteBlock
extern IReadBlock

var VDB 0

var Fixed 0

const MaxParts 8

externconst DiskNode

procedure LoadVDBSafe { -- ok }
	1 ok!

	LoadVDB

	if (VDBValid ~~)
		VDBFix dup Fixed! ok!
	end
end

procedure LoadVDB (* -- *)
	4096 Malloc VDB!

	0 VDB@ IReadBlock
end

procedure FreeVDB (* -- *)
	VDB@ Free
end

procedure VDBValid { -- valid }
	VDB@ VDB_Magic + @ 0x4E4D494C == valid!
end

procedure VDBLabel { -- label }
	VDB@ VDB_Label + label!
end

procedure VDBFix { -- ok }
	if (VDBValid) 1 ok! return end

	auto ynr

	"this disk's volume descriptor block is corrupt or empty.\nwrite new one" PromptYN ynr!

	if (ynr@ ~~) 0 ok! return end

	auto nbuf
	16 Calloc nbuf!

	"disk label: " Printf

	nbuf@ 15 Gets

	auto buf
	4096 Calloc buf!

	0x4E4D494C buf@ VDB_Magic + !

	buf@ VDB_Label + nbuf@ strcpy

	"writing new disklabel...\n" Printf

	0 buf@ IWriteBlock

	buf@ Free
	nbuf@ Free

	FreeVDB
	LoadVDB

	"fixed disklabel successfully.\n\n" Printf

	1 ok! return
end

procedure PTEGet { ent -- label blocks status }
	VDB@ VDB_PartitionTable + ent@ PTE_SIZEOF * + ent!

	ent@ PTE_Label + label!
	ent@ PTE_Blocks + @ blocks!
	ent@ PTE_Status + @ status!
end

procedure PTESet { label blocks status ent -- }
	auto ptb
	VDB@ VDB_PartitionTable + ptb!
	ptb@ ent@ PTE_SIZEOF * + ptb!

	ptb@ PTE_Label + label@ strcpy
	blocks@ ptb@ PTE_Blocks + !
	status@ ptb@ PTE_Status + !
end

table PTStatus
	"unused"
	"boot"
	"used"
	"??? corrupt entry ???"
endtable

procedure PTInfo (* -- *)
	auto i
	0 i!

	while (i@ 8 <)
		auto status
		auto blocks
		auto label

		i@ PTEGet status! blocks! label!

		if (status@ 3 >)
			3 status!
		end

		if (status@ 0 ~=)
			label@ i@ "part%d: %s\n" Printf
			[status@]PTStatus@ "\tstatus: %s\n" Printf
			blocks@ dup 4096 * "\tsize: %d bytes (%d blocks)\n" Printf
		end

		1 i +=
	end
end

procedure PartitionDisk (* -- *)
	if (LoadVDBSafe ~~)
		"disklabel invalid. cannot continue.\n" Printf
		return
	end

	if (Fixed@ ~~)
		if ("dump current partition info" PromptYN)
			"\ncurrent partition info:\n" Printf
			PTInfo
			'\n' Putc
		end
	end

	auto blocksleft

	DiskNode@ a3xDeviceSelectNode
		"blocks" a3xDGetProperty 2 - blocksleft!
	a3xDeviceExit

	auto ptr
	VDB@ VDB_PartitionTable + ptr!

	ptr@ 128 0 memset

	auto buf
	256 Calloc buf!

	"number of partitions: " Printf

	buf@ 255 Gets

	auto partnum
	buf@ atoi 8 min partnum!

	auto i
	0 i!

	while (i@ partnum@ <)
		if (blocksleft@ 0 ==)
			"no more space on disk\n" Printf
			break
		end

		partnum@ 1 - i@ "partition %d of %d\n" Printf

		blocksleft@ 4096 * "  [%d bytes left] size: " Printf
		buf@ 255 Gets

		auto sz
		buf@ atoi sz!

		if (sz@ 0xFFF & 0 ~=)
			sz@ 0xFFFFF000 & 0x1000 + 4096 / sz!
		end else
			4096 sz /=
		end

		if (sz@ blocksleft@ >)
			"not enough space!\n" Printf
		end else
			sz@ blocksleft -=

			sz@ ptr@ PTE_Blocks + !

			"  label: " Printf
			buf@ 7 Gets

			ptr@ PTE_Label + buf@ strcpy

			2 ptr@ PTE_Status + !

			PTE_SIZEOF ptr +=

			1 i +=
		end
	end

	buf@ Free

	0 VDB@ IWriteBlock

	FreeVDB
end