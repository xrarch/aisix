#include "<df>/dragonfruit.h"
#include "<ll>/rtaisix/aisix.h"

fn private Usage { -- }
	"usage: mount [-ru] device mountpoint [fstype]\n" STDERR FPrintf
end

fn Main { ... -- ret }
	0 ret!

	auto i
	1 i!

	auto chk
	3 chk!

	auto flags
	0 flags!

	if (argc@ 2 >=)
		auto sw
		[1]argv@ sw!

		if (sw@ gb '-' ==)
			2 i!
			4 chk!

			1 sw +=

			while (sw@ gb 0 ~=)
				if (sw@ gb 'r' ==)
					FS_READONLY flags |=
				end elseif (sw@ gb 'u' ==)
					FS_NOUID flags |=
				end else
					Usage
					1 Exit
				end

				1 sw +=
			end
		end
	end

	if (argc@ chk@ <)
		Usage
		1 ret!
		return
	end

	if (argc@ chk@ 1 + >)
		Usage
		1 ret!
		return
	end

	auto fst
	"aisixfs" fst!

	if (argc@ chk@ 1 + ==)
		[i@ 2 +]argv@ fst!
	end

	auto dev
	[i@]argv@ dev!

	if (dev@ "nodev" strcmp)
		0 dev!
	end

	flags@ dev@ [i@ 1 +]argv@ fst@ Mount ret!

	if (ret@ iserr)
		ret@ abs ret!
		[ret@]ErrorNames@ "mount: %s\n" Abort
	end
end