#include "<df>/dragonfruit.h"
#include "<ll>/rta3x/a3x.h"

externptr DeviceType
extern PartitionDisk { -- }

extern LoadVDB { -- }
extern FreeVDB { -- }
extern VDBValid { -- valid }
extern PTInfo { -- }
extern VDBLabel { -- label }
extern VDBWrite { new -- }
extern WriteBoot { -- }

externptr DiskNode

table DiskTypes
	0
	"raw disk"
	"partition"
endtable

fn PromptYN { ... fmt -- r }
	"\[[7m" Puts

	argv argc@ fmt@ VPrintf
	" [y/n]? " Printf

	while (Getc dup r! ERR ==) end

	"\[[0m\[[2K\r" Puts

	r@ Putc

	'\n' Putc

	if (r@ 'y' ==)
		1 r! return
	end

	0 r! return
end

fn PromptON { ... prompt -- num }
	'\n' Putc

	prompt@ Puts

	auto i
	1 i!
	while (i@ argc@ <=)
		[i@ 1 -]argv@ i@ "\t%d. %s\n" Printf
		i@ 1 + i!
	end

	'\n' Putc

	auto r
	12 Calloc r!

	"option #: " Printf

	r@ 11 Gets

	r@ atoi num!

	r@ Free
end

fn PromptAnyKey { -- }
	"\[[7mpress any key\[[0m" Printf

	while (Getc -1 ==) end

	"\[[2K\r" Puts
end

fn FormatDisk { -- }
	"NOT YET IMPLEMENTED!!!\n" Printf

	PromptAnyKey
end

fn Prompt { -- }
	auto r
	0 r!

	auto dfo

	while (r@ 6 > r@ 0 == ||)
		"write bootloader"
		"write new disklabel (erase all partitions)"
		"partition the disk"
		"format the volume with a new filesystem"
		"view volume info"
		"exit (preserve data)"
		"which would you like to do?\n"
		PromptON r!
	end

	if (r@ 1 ==)
		0 a3xReturn
	end

	if (r@ 2 ==)
		'\n' Putc

		[DeviceType@]DiskTypes@ "\[[33mtype:\[[0m \[[94m%s\[[0m\n" Printf

		DiskNode@ a3xDeviceSelectNode
			"blocks" a3xDGetProperty 4096 * 1024 / "\[[33msize:\[[0m \[[94m%dkb\[[0m\n" Printf
			"offset" a3xDGetProperty 4096 * "\[[33mlogical offset:\[[0m \[[94m%d bytes\[[0m\n" Printf

			auto osl
			"osLabel" a3xDGetProperty osl!

			if (osl@ 0 ~=)
				osl@ "\[[33minstalled OS:\[[0m \[[94m%s\[[0m\n" Printf
			end
		a3xDeviceExit

		if (DeviceType@ 1 ==)
			LoadVDB

			if (VDBValid)
				VDBLabel "\[[33mdisk label:\[[0m \[[94m%s\[[0m\n" Printf

				PTInfo
				FreeVDB
			end else
				"no valid disklabel\n" Printf
			end
		end

		PromptAnyKey
	end elseif (r@ 6 ==)
		1 dfo!

		if (DeviceType@ 1 ==)
			"\nyou are trying to write boot blocks to a raw disk. this could destroy\nrandom data. you probably want to write them to a partition instead.\n" Printf

			if ("are you sure that this is what you want" PromptYN ~~)
				0 dfo!
			end 
		end

		if (dfo@)
			WriteBoot
		end
	end elseif (r@ 5 ==)
		1 VDBWrite
	end elseif (r@ 4 ==)
		if (DeviceType@ 1 ~=)
			"\ncan only partition a raw disk. run this utility again, but supply the raw disk\npath. you are attempting to partition a partition.\n" Printf
			return
		end

		PartitionDisk
	end elseif (r@ 3 ==)
		1 dfo!

		if (DeviceType@ 1 ==)
			"\nyou are trying to put a filesystem on a raw disk. this will destroy\nany existing partition table. you probably want to put this filesystem\non a partition instead.\n" Printf

			if ("are you sure that this is what you want" PromptYN ~~)
				0 dfo!
			end 
		end

		if (dfo@)
			FormatDisk
		end
	end
end